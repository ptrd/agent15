apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    def junitVersion = "5.10.2"

    // https://mvnrepository.com/artifact/at.favre.lib/hkdf
    implementation group: 'at.favre.lib', name: 'hkdf', version: '2.0.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-params:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation("org.mockito:mockito-core:5.14.1")
    testImplementation("org.assertj:assertj-core:3.26.3")
    testImplementation("org.bouncycastle:bcprov-jdk18on:1.78.1")
}

sourceSets {
    main {
        resources {
            srcDir "${buildDir}/generated"
            exclude 'sources/**'
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'tech.kwik.agent15.Version'
    }
}

task determineVersion {
    doLast {
        def proc = "git describe --always --dirty".execute()
        proc.waitFor()
        if (proc.exitValue()) {
            logger.quiet("Building development version.")
        }
        else {
            def versionTag = proc.in.readLines()[0]
            // Check that tag matches format like v1.0 or v1.0.19
            if (versionTag ==~ /v?[0-9]+(\.\d+){1,2}/) {
                if (versionTag.startsWith("v")) {
                    versionTag = versionTag[1..-1]
                }
                logger.quiet("Building version ${versionTag}.")
            }
            else {
                logger.error("Version tag '${versionTag}' does not contain a valid version.");
            }
            project.version = versionTag

            ext.versionDir = new File("${buildDir}", "generated/tech/kwik/agent15")
            versionDir.mkdirs()

            new File(versionDir, "version.properties").text = versionTag
        }
    }
}

compileJava.dependsOn (determineVersion, processResources)

tasks.compileJava {
    options.javaModuleMainClass = "tech.kwik.agent15.Version"
}

group = 'tech.kwik'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        library(MavenPublication) {
            artifactId = 'agent15'
            from components.java
            pom {
                name = 'Agent15'
                description = 'A (partial) TLS 1.3 implementation in Java, suitable and intended for use in a QUIC implementation.'
                url = 'https://github.com/ptrd/agent15'

                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0-standalone.html'
                    }
                }

                developers {
                    developer {
                        id = 'peterd'
                        name = 'Peter Doornbosch'
                        email = 'peter.doornbosch@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git@github.com:ptrd/agent15.git'
                    developerConnection = 'scm:git:git@github.com:ptrd/agent15.git'
                    url = 'https://github.com/ptrd/agent15'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties.ossrhUsername
                password = project.properties.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.library
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addBooleanOption('html5', true)
}
